group 'lunivore.com'
version '0.0.2-20170512'

buildscript {
    ext.kotlinVersion = '1.1.1'
    ext.junitVersion  = '4.12'
    ext.mockitoVersion = '1.10.19'
    ext.testFxVersion = '4.0.6-alpha'
    ext.openCsvVersion = '3.9'
    ext.kodeinVersion = '3.4.1'
    ext.stirryVersion = '0.0.1-20170509'
    ext.cucumberVersion = '1.2.5'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

sourceSets {
    scenarios {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/scenarios/kotlin')
            resources.srcDirs = ["src/scenarios/resources"]
        }
    }
}

//// Using Stirry from Github for scenarios
//
// I would love to use an Ivy patterned repository instead. Unfortunately Ivy calls HEAD,
// which causes Github to return a 403 error "Forbidden". Copying it to a local lib directory
// is the next best thing. Maybe one day Stirry will be releasable too.
////
clean {
    delete("libs")
}
task("copyStirry") {
    if(!file("libs").exists()) { file("libs").mkdir() }
    if(!file("libs/stirry-${stirryVersion}.jar").exists()) {
        ant.get(src: "http://github.com/lunivore/Stirry/releases/download/$stirryVersion/stirry-${stirryVersion}.jar",
                dest: "libs",
                skipexisting: true)
    }
}
scenariosClasses.dependsOn("copyStirry")
// End using Stirry from Github

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "com.opencsv:opencsv:$openCsvVersion"
    compile "com.github.salomonbrys.kodein:kodein:$kodeinVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.mockito:mockito-all:$mockitoVersion"
    testRuntime "junit:junit:$junitVersion"

    scenariosCompile "junit:junit:$junitVersion"
    scenariosCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    scenariosCompile "com.opencsv:opencsv:$openCsvVersion"
    scenariosCompile "com.github.salomonbrys.kodein:kodein:$kodeinVersion"
    scenariosCompile "com.lunivore.stirry:stirry:$stirryVersion"
    scenariosCompile "info.cukes:cucumber-junit:$cucumberVersion"
    scenariosCompile "info.cukes:cucumber-java8:$cucumberVersion"
    scenariosCompile "info.cukes:cucumber-picocontainer:$cucumberVersion"

    scenariosRuntime "junit:junit:$junitVersion"
    scenariosRuntime "com.lunivore.stirry:stirry:$stirryVersion"
    scenariosRuntime "com.github.salomonbrys.kodein:kodein:$kodeinVersion"
}

jar {
    from {
        configurations.runtime.collect { it.isDirectory()? it : zipTree(it) }
    }{
        exclude "META-INF/*.*"
        exclude "META-INF/maven/**"

        // I can't work out why these appear in my jar in the first place.
        // They don't appear in my dependency tree.
        exclude "org/intellij/**"
        exclude "org/jetbrains/**"
    }
    from('.') {
        include 'LICENSE*.txt'
    }
    manifest {
        attributes 'Main-Class': 'com.lunivore.montecarluni.Montecarluni'
    }
}